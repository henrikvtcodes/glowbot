plugins {
    id 'java'
    id "com.diffplug.spotless" version "6.11.0"
    id "com.github.spotbugs" version "5.0.13"

    id "edu.wpi.first.GradleRIO" version "2023.4.3"
    id "edu.wpi.first.wpilib.repositories.WPILibRepositoriesPlugin" version "2020.2"

    id "maven-publish" apply true

}

project.group = 'com.github.FRC9015'
group = 'com.github.FRC9015'


sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11


configurations {
    jaxDoclet
}

repositories {
    mavenCentral()
    mavenLocal()
    maven {url "https://jitpack.io/"}
}

dependencies {
    implementation wpi.java.deps.wpilib()
    implementation wpi.java.vendor.java()

    nativeDebug wpi.java.deps.wpilibJniDebug(wpi.platforms.desktop)
    nativeDebug wpi.java.vendor.jniDebug(wpi.platforms.desktop)
    simulationDebug wpi.sim.enableDebug()

    nativeRelease wpi.java.deps.wpilibJniRelease(wpi.platforms.desktop)
    nativeRelease wpi.java.vendor.jniRelease(wpi.platforms.desktop)
    simulationRelease wpi.sim.enableRelease()

    compileOnly 'org.projectlombok:lombok:1.18.26'
    annotationProcessor 'org.projectlombok:lombok:1.18.26'

    testCompileOnly 'org.projectlombok:lombok:1.18.26'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.26'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

publishing {
   publications {
       mavenJava(MavenPublication) {
        groupId = 'frc.qefrc'
        artifactId = 'qelib'
        //    version = "${version}"

           from components.java
       }
   }
}

test {
    useJUnitPlatform()
}

final YEAR = new Date().format('yyyy')

spotless {
    java {
        licenseHeader """
/* Copyright (c) ${YEAR} Urban Inspire Corp 501(c)3 d.b.a. Questionable Engineering. All rights reserved. */
/* This work is licensed under the terms of the MPL 2.0 license */
/* found in the root directory of this project. */
"""

        googleJavaFormat('1.13.0').aosp()

        trimTrailingWhitespace()
        endWithNewline()

        importOrder('frc.qefrc.qelib', 'frc.qefrc', 'edu.wpi')
        removeUnusedImports()
    }
}

spotbugs {
   ignoreFailures = true
   effort = 'max'
}


javadoc {
    destinationDir = file('docs/')
    failOnError = false
    options.links("https://docs.oracle.com/en/java/javase/11/docs/api/")
    options.links("https://github.wpilib.org/allwpilib/docs/release/java/")
}

java {
    // Make the java task build a javadoc and source jar
    withJavadocJar()
    withSourcesJar()

    // Force the project to compile for Java 11
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

build {
    dependsOn javadoc
}

wrapper {
    gradleVersion = "7.4"
    distributionType = Wrapper.DistributionType.ALL
}